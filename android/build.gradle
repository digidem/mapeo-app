// Top-level build file where you can add configuration options common to all sub-projects/modules.

// Load the .env file in the repo and set project environment variables if found
def loadDotEnv() {
    def envFile = ".env"

    def env = [:]
    println("Reading env from: $envFile")

    File f = new File("$project.rootDir/../$envFile");
    if (!f.exists()) {
        f = new File("$envFile");
    }

    if (f.exists()) {
        f.eachLine { line ->
            def matcher = (line =~ /^\s*(?:export\s+|)([\w\d\.\-_]+)\s*=\s*['"]?(.*?)?['"]?\s*$/)
            if (matcher.getCount() == 1 && matcher[0].size() == 3) {
                env.put(matcher[0][1], matcher[0][2].replace('"', '\\"'))
            }
        }
    } else {
        println("**************************")
        println("*** Missing .env file ****")
        println("**************************")
    }

    project.ext.set("env", env)
}

def getenv(key) {
    return System.getenv(key) ?: project.env.get(key)
}

loadDotEnv()

buildscript {
    ext {
        buildToolsVersion = "30.0.2"
        // Android 5.0 - requirement for expo unimodules
        minSdkVersion = 21
        compileSdkVersion = 30
        targetSdkVersion = 30
        supportLibVersion = "30.0.0"
        // Kotlin is needed by Detox testing framework and react-native-screens
        kotlinVersion = "1.6.10"
        ndkVersion = "21.4.7075529"
    }
    repositories {
        google()
        mavenCentral()
        maven { url("$rootDir/../node_modules/react-native/android") }
        maven { url("https://www.jitpack.io") }
        maven { url("https://maven.google.com") }
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.2.2")
        // Bugsnag plugin for uploading source maps and symbols to bugsnag
        classpath 'com.bugsnag:bugsnag-android-gradle-plugin:5.+'
        // Used in ./app/build.gradle to parse app version for Bugsnag
        classpath 'net.swiftzer.semver:semver:1.2.0'
        // Used by Detox
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        // Basically removes references to jcenter in deps,
        // which is needed due to jcenter officially shutting down
        // https://github.com/facebook/react-native/issues/35136
        all { ArtifactRepository repo ->
            if (repo instanceof MavenArtifactRepository) {
                def url = repo.url.toString()
                if (url.startsWith('https://jcenter.bintray.com/')) {
                    project.logger.lifecycle "Repository ${repo.url} dropped"
                    remove repo
                    // TODO: We may want to call mavenCentral() here based on the issue,
                    // but not sure it's necessary
                }
            }
        }
        mavenLocal()
        maven { url("https://maven.google.com") }
        maven {
            // Replace AAR from original RN with AAR from react-native-v8
            url("$rootDir/../node_modules/react-native-v8/dist")
        }
        maven {
            // prebuilt libv8.so
            url("$rootDir/../node_modules/v8-android/dist")
        }
        maven {
            // expo-camera bundles a custom com.google.android:cameraview
            url("$rootDir/../node_modules/expo-camera/android/maven")
        }
        google()
        mavenCentral()
        // Needed for installing @react-native-mapbox-gl/maps, which relies on the v9 Maps SDK
        // Context: https://github.com/react-native-mapbox-gl/maps/issues/1572
        // Instructions for installation:
        // - https://github.com/rnmapbox/maps/blob/38c4cc6cc50583ebbc488d5142e11f0132c9db2e/android/install.md#mapbox-maps-sdk-pre-v10
        // - https://docs.mapbox.com/android/maps/guides/install/#configure-credentials
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                // Do not change the username below.
                // This should always be `mapbox` (not your username).
                username = "mapbox"
                // Use the secret token you stored in user's gradle.properties (i.e. $HOME/.gradle/gradle.properties) as the password
                password = getenv("MAPBOX_DOWNLOAD_TOKEN") ?: ""
            }
        }
        maven { url 'https://www.jitpack.io' }
        maven {
            // All of Detox' artifacts are provided via the npm module
            url "$rootDir/../node_modules/detox/Detox-android"
        }
    }
}
